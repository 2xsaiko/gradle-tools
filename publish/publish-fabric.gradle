apply plugin: 'java'
apply plugin: 'maven-publish'

String gid = System.getenv("PUBLISH_GROUP") ?: project.group.toString()
String aid = System.getenv("PUBLISH_ARTIFACT") ?: project.archivesBaseName
String buildid = System.getenv("PUBLISH_BUILD")
String ver = project.version.toString()
String mavenUrl = System.getenv("PUBLISH_REPO")

if (buildid != null) {
  ver += "-$buildid"
}

println("publish: $gid:$aid:$ver")
println("maven repo path: ${mavenUrl ?: "<unset>"}")

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId = gid
      artifactId = aid
      version = ver

      artifact(jar, {
        builtBy remapJar
      })
      artifact(sourcesJar, {
        builtBy remapSourcesJar
      })
    }
  }
  if (mavenUrl != null) {
    repositories {
      maven {
        url = mavenUrl
      }
    }
  }
}
